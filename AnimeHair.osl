color sigmoid(float x, color low, color high, float threshold, float smooth) {
    return low+(high-low)*(1.0/(1+exp((threshold-x)*smooth)));
}

shader Anime_Hair(
    float DiffFactor=1,
    color DiffColor=0.3,
    color DiffShadow=0.1,
    float ShadowThres=0.3,
    color DiffLight=0.5,
    float LightThres=0.7,
    float DiffSmooth=1,
    float SpecFactor=0.2,
    color SpecColor=0.1,
    float SpecU=0.6,
    float SpecV=0.2,
    normal Normal=N,
    float Rotation=90,
    output closure color BSDF = diffuse(N))
{
    color Colorset[8]={0,0,0,0,0,0,0,0};
    for (int i=0;i<4;i++) Colorset[i]=sigmoid(i*0.143, DiffFactor*DiffShadow+(1-DiffFactor)*DiffColor, DiffColor, ShadowThres, DiffSmooth) ;
    for (int i=4;i<8;i++) Colorset[i]=sigmoid(i*0.143, DiffColor, DiffFactor*DiffLight+(1-DiffFactor)*DiffColor, LightThres, DiffSmooth) ;
    float rad = radians(Rotation);
    vector Tan = vector( Normal[0]*cos(rad)-Normal[1]*sin(rad), Normal[0]*sin(rad)+Normal[1]*cos(rad), 0);
    BSDF = diffuse_ramp(Normal,Colorset) + SpecFactor*SpecColor*ashikhmin_shirley(Normal, Tan, SpecU, SpecV);
}